{-# OPTIONS --without-K #-}
module HoTT.Identity where

open import HoTT.Types
open import HoTT.Empty using (Â¬_)

=-ind : âˆ€ {i j} {A : ğ’° i} â†’
        (C : (x y : A) â†’ x == y â†’ ğ’° j) â†’ ((x : A) â†’ C x x refl) â†’ (x y : A) â†’ (p : x == y) â†’ C x y p
=-ind C c x .x refl = c x

-- Based path induction
=-ind' : âˆ€ {i j} {A : ğ’° i} â†’
         (a : A) â†’ (C : (x : A) â†’ a == x â†’ ğ’° j) â†’ C a refl â†’ (x : A) â†’ (p : a == x) â†’ C x p
=-ind' a C c .a refl = c

_â‰ _ : âˆ€ {i} {A : ğ’° i} â†’ A â†’ A â†’ ğ’° i
_â‰ _ x y = Â¬ (x == y)

-- Lemma 2.1.1
_â»Â¹ : âˆ€ {i} {A : ğ’° i} {x y : A} â†’ x == y â†’ y == x
_â»Â¹ refl = refl
infix 30 _â»Â¹

{- Induction proof
_â»Â¹ {i} {A} {x} {y} p = =-ind D d x y p
  where
    D : (x y : A) â†’ x == y â†’ ğ’° i
    D x y p = y == x
    d : (x : A) â†’ D x x refl
    d x = refl
-}

-- Lemma 2.1.2
_âˆ™_ : âˆ€ {i} {A : ğ’° i} {x y z : A} â†’ x == y â†’ y == z â†’ x == z
_âˆ™_ refl refl = refl
infixl 20 _âˆ™_

{- Induction proof
_âˆ™_ {i} {A} {x} {y} {z} p q = =-ind D d x y p z q where
  E : (x z : A) (q : x == z) â†’ ğ’° i
  E x z q = x == z
  e : (x : A) â†’ E x x refl
  e x = refl
  D : (x y : A) â†’ x == y â†’ ğ’° i
  D x y p = (z : A) (q : y == z) â†’ x == z
  d : (x : A) â†’ D x x refl
  d x z q = =-ind E e x z q
-}

-- Lemma 2.1.4
--  (i)
ru : âˆ€ {i} {A : ğ’° i} {x y : A} (p : x == y) â†’ p == p âˆ™ refl
ru {x = x} {y} p = =-ind (Î» _ _ p â†’ p == p âˆ™ refl) (Î» _ â†’ refl) x y p

lu : âˆ€ {i} {A : ğ’° i} {x y : A} (p : x == y) â†’ p == refl âˆ™ p
lu {x = x} {y} p = =-ind (Î» _ _ p â†’ p == refl âˆ™ p) (Î» _ â†’ refl) x y p

--  (ii)
=-linv : âˆ€ {i} {A : ğ’° i} {x y : A} (p : x == y) â†’ p â»Â¹ âˆ™ p == refl
=-linv {x = x} {y} p = =-ind (Î» _ _ p â†’ p â»Â¹ âˆ™ p == refl) (Î» _ â†’ refl) x y p

=-rinv : âˆ€ {i} {A : ğ’° i} {x y : A} (p : x == y) â†’ p âˆ™ p â»Â¹ == refl
=-rinv {x = x} {y} p = =-ind (Î» _ _ p â†’ p âˆ™ p â»Â¹ == refl) (Î» _ â†’ refl) x y p

--  (iv)
assoc : âˆ€ {i} {A : ğ’° i} {x y z w : A} (p : x == y) (q : y == z) (r : z == w) â†’
        p âˆ™ (q âˆ™ r) == (p âˆ™ q) âˆ™ r
assoc refl refl refl = refl

{- Induction proof
assoc {i} {A} {x} {y} {z} {w} p q r = =-ind Dâ‚ dâ‚ x y p z w q r where
  Dâ‚ƒ : (z w : A) â†’ (r : z == w) â†’ ğ’° i
  Dâ‚ƒ _ _ r = refl âˆ™ (refl âˆ™ r) == (refl âˆ™ refl) âˆ™ r
  dâ‚ƒ : (z : A) â†’ Dâ‚ƒ z z refl
  dâ‚ƒ _ = refl

  Dâ‚‚ : (y z : A) â†’ (q : y == z) â†’ ğ’° i
  Dâ‚‚ _ z q = (w : A) â†’ (r : z == w) â†’ refl âˆ™ (q âˆ™ r) == (refl âˆ™ q) âˆ™ r
  dâ‚‚ : (z : A) â†’ Dâ‚‚ z z refl
  dâ‚‚ z w r = =-ind Dâ‚ƒ dâ‚ƒ z w r

  Dâ‚ : (x y : A) â†’ (p : x == y) â†’ ğ’° i
  Dâ‚ _ y p = (z w : A) â†’ (q : y == z) â†’ (r : z == w) â†’ p âˆ™ (q âˆ™ r) == (p âˆ™ q) âˆ™ r
  dâ‚ : (y : A) â†’ Dâ‚ y y refl
  dâ‚ y z w q r = =-ind Dâ‚‚ dâ‚‚ y z q w r
-}

-- Lemma 2.2.1
ap : âˆ€ {i j} {A : ğ’° i} {B : ğ’° j} {x y : A}
     (f : A â†’ B) â†’ x == y â†’ f x == f y
ap f refl = refl

{- Induction proof
ap {x = x} {y} f p = =-ind (Î» x y _ â†’ f x == f y) (Î» _ â†’ refl) x y p
-}

-- Lemma 2.3.1
transport : âˆ€ {i j} {A : ğ’° i} {P : A â†’ ğ’° j} {x y : A} â†’
            x == y â†’ P x â†’ P y
transport refl = id

{- Induction proof
transport {P = P} {x} {y} p = =-ind (Î» x y _ â†’ P x â†’ P y) (Î» _ â†’ id) x y p
-}

-- Lemma 2.3.4
apd : âˆ€ {i j} {A : ğ’° i} {P : A â†’ ğ’° j} {x y : A}
      (f : (x : A) â†’ P x) (p : x == y) â†’ transport p (f x) == f y
apd f refl = refl

{- Induction proof
apd {x = x} {y} f p = =-ind (Î» x y p â†’ transport p (f x) == f y) (Î» _ â†’ refl) x y p
-}

-- Lemma 2.3.5
transportconst : âˆ€ {i j} {A : ğ’° i} {B : ğ’° j} {x y : A}
                 (p : x == y) (b : B) â†’ transport p b == b
transportconst refl _ = refl
